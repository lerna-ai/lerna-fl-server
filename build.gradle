import java.time.LocalDateTime
import java.time.format.DateTimeFormatter

plugins {
	id 'org.springframework.boot' version '2.6.1'
	id 'io.spring.dependency-management' version '1.0.11.RELEASE'
	id 'com.github.jk1.dependency-license-report' version '2.0'
	id 'java'
	id 'groovy'
}

description = "Lerna FL API"
group = 'ai.lerna'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '8'

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.postgresql:postgresql'
	implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-xml:2.13.0'
	implementation 'com.fasterxml.jackson.datatype:jackson-datatype-joda:2.13.0'
	implementation 'org.springdoc:springdoc-openapi-ui:1.6.1'
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-data-redis'
	implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation 'org.springframework.boot:spring-boot-starter-thymeleaf:2.6.1'
	implementation 'org.springframework.cloud:spring-cloud-aws-messaging:2.2.0.RELEASE'
	implementation 'io.jsonwebtoken:jjwt-api:0.11.2'
	runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.11.2',
			// Uncomment the next line if you want to use RSASSA-PSS (PS256, PS384, PS512) algorithms:
			//'org.bouncycastle:bcprov-jdk15on:1.60',
			'io.jsonwebtoken:jjwt-jackson:0.11.2' // or 'io.jsonwebtoken:jjwt-gson:0.11.2' for gson
	implementation 'javax.xml.bind:jaxb-api'
	implementation 'redis.clients:jedis:3.7.0'
	implementation 'org.flywaydb:flyway-core:5.2.4'
	implementation 'org.nd4j:nd4j-native-platform:1.0.0-beta2'
	implementation 'org.nd4j:nd4j-jackson:1.0.0-beta2'
	testImplementation 'org.codehaus.groovy:groovy-all:3.0.8'
	testImplementation 'org.spockframework:spock-core:2.0-groovy-3.0'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

jar {
	enabled = false
}

test {
	useJUnitPlatform()
}

tasks.register("generateMigration") {
	group = "Flyway"
	description = "Create a new flyway migration script file"

	String scriptName = "${project.findProperty("flywayScriptName") ?: ""}"
	if (scriptName != "") {
		LocalDateTime now = LocalDateTime.now()
		String nowStr = now.format(DateTimeFormatter.ofPattern("YYYYMMddHHmmSSS"))
		String filename = "V${nowStr}__${scriptName}.sql"

		File file = file("src/main/resources/db/migration/${filename}")
		file.createNewFile()
	}
}
